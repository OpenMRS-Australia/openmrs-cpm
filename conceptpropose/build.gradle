
group = 'org.openmrs.module.conceptpropose'
version = '1.0-SNAPSHOT'

subprojects {
    dependencies {
        compile project(':common')
    }
}

dependencies {
    releaseJars(project(':common')) {
        transitive = false
    }
}

task createOmod(type: Jar, dependsOn: subprojects.tasks['build']) {

    baseName = 'openmrs-module-' + project.name
    extension = 'omod'

    subprojects.each {
        subproject ->
            from subproject.configurations.archives.allArtifacts.files.collect {
                zipTree(it)
            }

    }

    into('web/module') {
        from fileTree(dir: 'omod/src/main/webapp')
    }

    into('lib') {
        from configurations.releaseJars
    }

    metaInf {
        into('maven/org.openmrs.module/' + project.name + '-omod')
        def pomProperties = new File(getTemporaryDir(), 'pom.properties')
        pomProperties.write "version=${project.version}\n"
        pomProperties << "groupId=${project.group}\n"
        pomProperties << "artifactId=${project.name}\n"
        from pomProperties
    }
}

artifacts {
    archives createOmod
}

defaultTasks 'createOmod'
